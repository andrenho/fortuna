;---------------------------------------
; LOAD SECTOR IN EXCHANGE AREA
;---------------------------------------

; Load a sector into SDCard exchange area
;  - HL: LSB sector number
;  - DE: MSB sector number
;  - returns A -> 0 = success

fat16_load_sector:
        ld      a, l                    ; access SDCard
        out     (I_SD_B0), a
        ld      a, h
        out     (I_SD_B1), a
        ld      a, e
        out     (I_SD_B2), a
        ld      a, d
        out     (I_SD_B3), a
        ld      a, SD_READ
        out     (I_SD_ACTION), a
        in      a, (I_SD_STATUS)
        ret


;---------------------------------------
; LOAD CLUSTER IN EXCHANGE AREA
;---------------------------------------

; Load a cluster into SDCard exchange area
;  - HL: cluster number
;  - A: sector number
;  - returns A -> 0 = success

fat16_load_cluster:
        ld      b, a                    ; DEHL = cluster * sectors_per_cluster
        ld      a, (fat16_sectors_per_cluster)
        ld      d, h
        ld      e, l
        push    bc
        call    mult_a_de
        pop     bc
        ld      d, 0
        ld      e, a
        ld      a, b

        ld      b, 0                    ; + sector number
        ld      c, a
        add     hl, bc
        jp      nc, .not_carry
        inc     de
.not_carry:

        call    fat16_load_sector       ; load sector
        ret

;---------------------------------------
; LOAD DATA CLUSTER IN EXCHANGE AREA
;---------------------------------------

; Load a cluster into SDCard exchange area, starting from data sector
;  - HL: cluster number
;  - A: sector number
;  - returns A -> 0 = success

fat16_load_data_cluster:
        ld      b, h                            ; BC = cluster number
        ld      c, l
        ld      hl, (fat16_dataregion_cluster)  ; HL = dataregion cluster start
        add     hl, bc
        ld      bc, -2                          ; N - 2
        add     hl, bc
        call    fat16_load_cluster
        ret

; vim: ts=8:sts=8:sw=8:expandtab
