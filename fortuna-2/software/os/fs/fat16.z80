;---------------------------------------
; CONSTANTS
;---------------------------------------

FAT16_BYTES_PER_SECTOR       = 0x0b
FAT16_SECTORS_PER_CLUSTER    = 0x0d
FAT16_RESERVED_SECTORS       = 0x0e
FAT16_NUMBER_OF_FATS         = 0x10
FAT16_NUMBER_OF_ROOT_ENTRIES = 0x11
FAT16_SECTORS_PER_FAT        = 0x16

;---------------------------------------
; VARIABLES
;---------------------------------------

fat16_sectors_per_cluster       db      0
fat16_reserved_sectors          dw      0
fat16_number_of_fats            db      0
fat16_sectors_per_fat           dw      0
fat16_rootdir_start             dw      0
fat16_rootentries_count         dw      0
fat16_dataregion_start          dw      0
fat16_fat_start = fat16_reserved_sectors

current_dir_str                 db      '/'
                                fill    254
current_dir_sector_ptr          dw      0
current_dir_fat_ptr             dw      0

;---------------------------------------
; INITIALIZATION
;---------------------------------------

;
; Initialize file system - read config information about the partition
;

fat16_initialize:

        ;
        ; load sector 0 into exchange area
        ;

        ld      de, 0                           ; load sector 0 into exchange area
        ld      hl, 0
        call    fat16_load_sector

        cp      0                               ; check if successful
        jr      z, .load_ok

        ld      hl, .sdcard_load_error          ; error if not
        SYS     _PRINT
        jr      .halt
.load_ok:

        ;
        ; makes sure that bytes per sector and number of root entries is 512
        ;

        ld      hl, (FAT16_BYTES_PER_SECTOR)
        ld      de, .sdcard_bps_not_512
        call    .confirm_512
        ld      hl, (FAT16_NUMBER_OF_ROOT_ENTRIES)
        ld      de, .sdcard_nre_not_512
        call    .confirm_512

        ;
        ; fill out the variables
        ;

        ld      a, (FAT16_SECTORS_PER_CLUSTER)
        ld      (fat16_sectors_per_cluster), a
        
        ld      hl, (FAT16_RESERVED_SECTORS)
        ld      (fat16_reserved_sectors), hl

        ld      a, (FAT16_NUMBER_OF_FATS)
        ld      (fat16_number_of_fats), a

        ld      hl, (FAT16_SECTORS_PER_FAT)
        ld      (fat16_sectors_per_fat), hl

        ; RootDirectory = FATRegion + (NumberOfFATs * SectorsPerFAT)
        ld      de, (fat16_sectors_per_fat)     ; NumberOfFats * SectorsPerFat
        ld      a, (fat16_number_of_fats)
        call    math_multiply
        ld      b, h                            ; FatRegion + result
        ld      c, l
        ld      hl, (fat16_reserved_sectors)
        add     hl, bc
        ld      (fat16_rootdir_start), hl

        ; DataRegionStart = RootDirectoryRegion + ((RootEntriesCount * 32) / BytesPerSector)
        ld      hl, (fat16_rootdir_start)       ; RootDirectoryRegion +
        ld      bc, 32                          ; (RootEntriesCount * 32 / BytesPerSector)
        add     hl, bc
        ld      (fat16_dataregion_start), hl

        ; setup current dir
        ld      hl, (fat16_rootdir_start)
        ld      (current_dir_sector_ptr), hl
        ld      hl, (fat16_fat_start)
        ld      (current_dir_fat_ptr), hl

        ret

        ; AUXILIARY LOCAL ROUTINES FOR INITIALIZATION

        ;
        ; Confirm that HL is 512, if not then print error
        ;  - HL: number to check
        ;  - DE: pointer to error message
        ;
.confirm_512:
        ld      a, h                            ; check MSB
        cp      2
        jr      nz, .error_512
        ld      a, l                            ; check LSB
        cp      0
        jr      nz, .error_512
        
        jr      .cont1                          ; if ok
.error_512:
        ld      h, d                            ; if not ok
        ld      l, e
        SYS     _PRINT
        jr      .halt
.cont1:
        ret

        ;
        ; halt
        ;
.halt:  jr      .halt

        ;
        ; error messages
        ;

.sdcard_load_error:     asciiz  "Error loading bootsector from SDCard."
.sdcard_bps_not_512:    asciiz  "FS error: bytes per sector should be 512."
.sdcard_nre_not_512:    asciiz  "FS error: number of root entries should be 512."

;---------------------------------------
; DIRECT SECTOR ACCESS
;---------------------------------------

; 
; Load a sector into SDCard exchange area
;  - HL: LSB sector number
;  - DE: MSB sector number
;  - returns A -> 0 = success
;

fat16_load_sector:
        ld      a, l
        out     (I_SD_B0), a
        ld      a, h
        out     (I_SD_B1), a
        ld      a, e
        out     (I_SD_B2), a
        ld      a, e
        out     (I_SD_B3), a
        ld      a, SD_READ
        out     (I_SD_ACTION), a
        in      a, (I_SD_STATUS)
        ret

; vim: ts=8:sts=8:sw=8:expandtab
